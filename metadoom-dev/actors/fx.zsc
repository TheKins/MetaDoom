/*
----------------------------
MetaDoom
Actor Definitions
Effects - Both Visual and Practical
----------------------------
*/

// Explosions of various kinds
class MetaExplosion : Actor
{
	Default
	{
	Radius 10;
	Height 10;
	Speed 5;
	PROJECTILE;
	+NOCLIP
	Decal "Scorch";
	Damagetype "Explosive";
	}
	States
	{
		Spawn:
		EXPL A 0;
		EXPL A 2 BRIGHT
		{
			A_PlaySound("weapons/explosion", CHAN_WEAPON);
			A_Explode();
			A_Quake (3,10,0,750,0);
			A_SpawnItem("Shockwave",0,0);
			
			if (pos.z <= (floorz + 64))
			{
				A_SpawnItem("ExplosionScorchMark",0,0);
			}
		}
		EXPL B 2 BRIGHT; 
		EXPL C 2 BRIGHT; 
		EXPL DEF 2;
		Stop;
	}
}

class Shockwave : Actor
{
	Default
	{
	radius 10;
	height 10;
	+NOCLIP
	+FLATSPRITE
	+NOGRAVITY
	+NOBLOCKMAP
	+ROLLCENTER
	Renderstyle "Add";
	}
	
	states
	{
		Spawn:
		SHOK A 0;
		SHOK A 0 A_SetAngle (random(0,365));
		//SHOK A 0 A_SetRoll (random(0,365))
		SHOK A 0 A_SetPitch(-32,32); //A_SetPitch(random(45,128)) // A_SetPitch(90)
	DoTheThing:
		SHOK A 1
		{
			A_FadeOut(0.05);
			A_SetScale(scale.X + 0.1);
		}
		loop;
	}
}

// Explosion Floor Decal
class ExplosionScorchMark : Actor
{
	Default
	{
	radius 1;
	height 1;
	+NOCLIP
	+FLATSPRITE
	+ROLLCENTER
	+DONTSPLASH
	Renderstyle "Shaded";
	StencilColor "00 00 00";
	}
	
	states
	{
		Spawn:
		SCRH A 0;
		SCRH A 0 A_SetAngle (random(0,365));
		SCRH A 0; //A_SetPitch(90)
		SCRH A 1 a_warp(aaptr_default, pos.x,pos.y,floorz + 0.01, 0, warpf_absoluteposition | WARPF_COPYINTERPOLATION);
		wait;
	}
}

// Smoke Trails
class RocketTrailSmoke : Actor
{
	Default
	{
	+NOBLOCKMAP
	+NOGRAVITY
	+DONTSPLASH
	}
	
	states
	{
	Spawn:
		PUF2 ABC 2 A_FadeOut;
	Finalize:
		PUF2 D 2 A_FadeOut;
		loop;
	}
}

class RocketFragmentTrailSmoke : RocketTrailSmoke
{
	Default
	{
	scale 0.25;
	}
}

class GrenadeTrailSmoke : RocketTrailSmoke
{
	Default
	{
	alpha 0.75;
	}
}

// Item Spawn FX
class MetaItemFog : Actor replaces ItemFog
{
	Default
	{
	+NOBLOCKMAP
	+NOGRAVITY
	}
	
	States
	{
	Spawn:
		IFOG A 0;
		IFOG A 0 A_PlaySound("misc/spawn");
		IFOG ABABCDE 6 Bright;
		Stop;
	}
}

// Zzzap! Ported over from JPF
class MetaZappy : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 16;
		Projectile;
		+RANDOMIZE
		+NOCLIP
		+CLIENTSIDEONLY
		+FORCEXYBILLBOARD
		+ROLLSPRITE
		+ROLLCENTER
		RenderStyle "Add";
		scale 0.5;
		Alpha 1;
	}
	States
	{
	Spawn:
		ZAPP A 0;
		ZAPP A 0 A_Stop;
		ZAPP A 0 A_SetRoll(random(0,360));
		ZAPP ABCD 0 A_Jump(256, "Death");
	Death:
		ZAPP A 1 bright
		{
			A_FadeOut(0.1);
			A_SetRoll(random(0,360));
		}
		loop;
	}
}