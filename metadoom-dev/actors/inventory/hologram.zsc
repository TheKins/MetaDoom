/*
----------------------------
MetaDoom
Actor Definitions
Holograms
----------------------------
*/

class HologramPickup : CustomInventory
{
	Default
	{
		inventory.pickupmessage "$GOTHOLOGRAM";
		Tag "$TAGHOLOGRAM";
		inventory.icon "INVHOLO";
		inventory.pickupsound "inventory/hologram/get";
		+INVENTORY.ALWAYSPICKUP
		scale 0.5;
		+BLOCKASPLAYER
		//$Category "MetaDoom Inventory Items"
		//$Title "Hologram"
		//$NotAngled
		//$Color 9
	}
	
	states
	{
	Spawn:
		HOLO Z -1;
		stop;
	Pickup:
		TNT1 A 0 A_GiveInventory ("Hologram", 1);
		//TNT1 A 0 A_GiveInventory ("GrenadeAmmo", 3);
		stop;
	}
}

class Hologram : CustomInventory
{
	Default
	{
		inventory.pickupmessage "$GOTHOLOGRAM";
		Tag "$TAGHOLOGRAM";
		inventory.icon "INVHOLO";
		inventory.pickupsound "inventory/hologram/get";
		inventory.amount 1;
		inventory.maxamount 1;
		+INVENTORY.INVBAR
		scale 0.5;
	}
	
	states
	{
	Spawn:
		HOLO Z -1;
		stop;
	Use:
		TNT1 A 0
		{
			if (CountInv("InventoryFuel_Hologram") >= INV_COOLDOWN_HOLOGRAM)
			{
				A_FireProjectile("HologramProj",0,0,0,0,FPF_TRANSFERTRANSLATION); //A_SpawnItemEx("HologramSam",32,0,8,0,0,0,0,SXF_TRANSFERTRANSLATION)
				A_TakeInventory("InventoryFuel_Hologram", 999);
			} else {
				A_PlaySound("menu/invalid");
			}
		}
		fail;
	}
}

class HologramProj : Actor
{
	Default
	{
		+CANBOUNCEWATER
		Radius 8;
		Height 8;
		Speed 10;
		Damage 0;
		PROJECTILE;
		+RANDOMIZE
		-NOGRAVITY
		+USEBOUNCESTATE
		+ROLLSPRITE
		BounceType "Doom";
		SeeSound "inventory/hologram/toss";
		BounceSound "inventory/grenade/bounce";
		-BOUNCEONFLOORS
		scale 0.3;
		ReactionTime 60;	//Just over five seconds
		gravity 0.75;
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 ThrustThingZ(0,24,0,1);
		TNT1 A 0 A_PlaySound("weapons/grenadethrow");
		goto Flying;
	Flying:
		HOLO Y 3 A_CountDown;
		loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("HologramSam",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION);
		Stop;
	}
}

class HologramSam : Actor			// you're my main man!
{
	Default
	{
		Radius 20;
		Height 56;
		Speed 8;
		+FLOORCLIP
		+SHOOTABLE
		+FRIENDLY
		+INVULNERABLE
		+NODAMAGE
		+NOBLOCKMAP
		-SOLID
		+NONSHOOTABLE
	}
	
	int user_seconds;
	
	States
	{
	Spawn:
		PLAY A 0;
		PLAY A 0 
		{
			A_PlaySound("inventory/hologram/on");
			Thing_ChangeTID (0, 3333);
			A_RadiusGive ("ForgetAboutDre", 2048, RGF_MONSTERS, 1);
			user_seconds = 0;
		}
		PLAY A 1 A_SetScale (0.1);
		PLAY A 1 A_SetScale (0.3,0.2);
		PLAY A 1 A_SetScale (0.4,0.5);
		PLAY A 1 A_SetScale (0.7,0.9);
		PLAY A 1 A_SetScale (1.2, 1.1);
		PLAY A 1 A_SetScale (1,1);
		goto Active;
	Active:
		PLAY A 0 A_AlertMonsters(0, AMF_TARGETEMITTER); // This really pisses 'em off.
		PLAY AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SetTranslucent(frandom(0.5,1.0),0);
		PLAY A 0 { user_seconds++; }
		PLAY A 0 A_JumpIf(user_seconds >= 10, "End");
		Loop;
	End:
		PLAY A 0 A_PlaySound("inventory/hologram/off");
		PLAY A 1 A_SetScale (0.9,1);
		PLAY A 1 A_SetScale (0.8,1);
		PLAY A 1 A_SetScale (0.75,1);
		PLAY A 1 A_SetScale (0.7,1);
		PLAY A 1 A_SetScale (0.6,1);
		PLAY A 1 A_SetScale (0.4,1);
		PLAY A 1 A_SetScale (0.1,1);
		PLAY A 1 A_SetScale (0.02,1);
		Stop;
	}
}

class ForgetAboutDre : CustomInventory
{
	states
	{
	Pickup:
		TNT1 A 0;
		TNT1 A 0 Thing_Hate (0, 3333, 0);
		stop;
	}
}