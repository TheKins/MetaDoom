/*
----------------------------
MetaDoom
Actor Definitions
Siphon Grenade
----------------------------
*/

// The bane of balance!

class SiphonGrenadePickup : CustomInventory
{
	Default
	{
		inventory.pickupmessage "$GOTSIPHON";
		Tag "$TAGSIPHON";
		inventory.icon "INVSIPH";
		inventory.pickupsound "inventory/siphongrenade/get";
		+INVENTORY.ALWAYSPICKUP;
		scale 0.5;
		+BLOCKASPLAYER
		//$Category "MetaDoom Inventory Items"
		//$Title "Siphon Grenade"
		//$NotAngled
		//$Color 9
	}
	
	states
	{
	Spawn:
		SIPH Z -1;
		stop;
	Pickup:
		TNT1 A 0 A_GiveInventory ("SiphonGrenade", 1);
		stop;
	}
}

class SiphonGrenade : CustomInventory
{
	Default
	{
		inventory.pickupmessage "$GOTSIPHON";
		Tag "$TAGSIPHON";
		inventory.icon "INVSIPH";
		inventory.pickupsound "inventory/siphongrenade/get";
		inventory.amount 1;
		inventory.maxamount 1;
		+INVENTORY.INVBAR
		scale 0.5;
	}
	
	states
	{
	Spawn:
		SIPH Z -1;
		stop;
	Use:
		TNT1 A 0
		{
			if (CountInv("InventoryFuel_SiphonGrenade") >= INV_COOLDOWN_SIPHON)
			{
				A_FireProjectile("SiphonGrenadeProj",0,0);
				A_TakeInventory("InventoryFuel_SiphonGrenade", 999);
			} else {
				A_PlaySound("menu/invalid");
			}
		}
		fail;
	}
}

class SiphonGrenadeProj : Actor
{
	Default
	{
		+CANBOUNCEWATER
		Radius 8;
		Height 8;
		Speed 20;
		Damage 0;
		PROJECTILE;
		+RANDOMIZE
		-NOGRAVITY
		//+USEBOUNCESTATE
		+ROLLSPRITE
		+FORCERADIUSDMG
		//BounceType "Doom"
		SeeSound "inventory/throw";
		//BounceSound "inventory/grenade/bounce"
		Obituary "$OB_SIPHON";
		//scale 0.5
		damagetype "Vampire";
		ReactionTime 60;	//Just over five seconds
	}
	
	States
	{
	Spawn:
		SIPH A 0;
		SIPH A 0
		{
			ThrustThingZ(0,32,0,1);
			A_PlaySound("weapons/grenadethrow");
		}
		goto Bounce;
	Bounce:
		"####" # 0 A_Jump(128, "Bounce2");
		goto Bounce1;
	Bounce1:
		"####" # 0 A_CountDown;
		"####" # 1 A_SetRoll((roll + 5));
		"####" # 1 A_SetRoll((roll + 5));
		"####" # 1 A_SetRoll((roll + 5));
		loop;
	Bounce2:
		"####" # 0 A_CountDown;
		"####" # 1 A_SetRoll((roll - 5));
		"####" # 1 A_SetRoll((roll - 5));
		"####" # 1 A_SetRoll((roll - 5));
		loop;
	Death:
		TNT1 A 0 
		{
			bNOGRAVITY = 1;
			A_PlaySound("inventory/siphongrenade/explode");
			A_SpawnItem("SiphonExplosionFX");
		}
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 
		{
			A_Explode((8), 100, !XF_HURTSOURCE);
			for (int i = 0; i < 4; i++)
			{
				A_SpawnItemEx("SiphonExplosionFXZappy",random(-100,100),random(-100,100),random(-100,100));
			}
		}
		Stop;
	}
}

class SiphonExplosionFX : Actor
{
	Default
	{
		Radius 10;
		Height 10;
		+NOBLOCKMAP
		-SOLID
		+NOGRAVITY
		+FORCEXYBILLBOARD
		+ROLLSPRITE
		renderstyle "Add";
	}
	
	States
	{
		Spawn:
			EXPL A 0;
			SIBM AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 bright 
			{
				A_SetScale(frandom(0.9,1.1),frandom(0.9,1.1));
				A_SetTranslucent(frandom(0.9,0.8), 1);
				//A_SetRoll(random(0,360));
				//A_SetAngle(random(0,360));
			}
		Die:
			SIBM AA 1 bright 
			{
				A_FadeOut(0.25);
				//A_SetRoll(random(0,360));
				//A_SetAngle(random(0,360));
			}
			loop;
	}
}

class SiphonExplosionFXZappy : MetaZappy
{
	Default
	{
		scale 1;
	}
}