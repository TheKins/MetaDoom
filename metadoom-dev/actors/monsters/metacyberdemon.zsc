/*
----------------------------
MetaDoom
Actor Definitions
Cyberdemon
----------------------------
*/

// Everyone's favorite boss monster. Uses the Doom 64 sprites, brightened up to
// fit within the original game. Has two new abilities so far:
// - Moving around while shooting, aim locked to target.
// - Turbo launch across the map.
// There's also a useless melee attack, I should prob. remove that?

class MetaCyberdemon : MonsterCore
{
	Default
	{
		Health 4000;
		Radius 40;
		Height 110;
		Mass 1000;
		Speed 16;
		PainChance 20;
		Monster;
		MinMissileChance 160;
		MeleeRange 64;
		+BOSS 
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		//+BOSSDEATH
		SeeSound "monsters/cyberdemon/see";
		PainSound "monsters/cyberdemon/pain";
		DeathSound "monsters/cyberdemon/die1";
		ActiveSound "monsters/cyberdemon/idle";
		Obituary "$OB_CYBORG";
		damagefactor "ChaingunTurret", 0.2;
		painchance "ChaingunTurret", 0;
		damagefactor "SpiderdemonAttack", 0.5;
		painchance "SpiderdemonAttack", 0;
		damagefactor "SpiderdemonTKAttack", 0.5;
		painchance "Vampire", 128;
		damagefactor "Vampire", 0.25;
		scale 0.75;
		MonsterCore.CanBeScared false;
		Tag "Cyberdemon";
		//$Category "Monsters/MetaDoom/Bosses"
		//$Title "Cyberdemon"
		//$Color 12
	}
	
	FLineTraceData SurfaceData;
	
	States
	{
	Spawn:
		CYBR AB 10 A_Look;
		Loop;
	See:
		CYBR F 0;
		CYBR F 0 A_Quake(5,30,0,2048,0);
		CYBR F 35 A_FaceTarget();
		goto Roam;
	Roam:
		CYBR A 3 { A_Chase(); A_PlaySound("monsters/cyberdemon/hoof"); }
		CYBR ABBCC 3 A_Chase();
		CYBR D 3 { A_Chase(); A_PlaySound("spider/walk"); }
		CYBR D 3 A_Chase();
		goto Roam;
	Missile:
		CYBR E 0;
		//CYBR E 0 A_Jump(256, "MissileCharge"); // for debug
		CYBR E 0 A_Jump(84, "MissileCharge");
		CYBR E 0 A_Jump(64, "MissileWalking");
		goto MissileStanding;
	// Standing attack
	MissileStanding:
		CYBR F 6 A_FaceTarget();
		CYBR E 12 BRIGHT
		{
			A_SpawnProjectile("CyberdemonRocket", 48, -16);
			A_PlaySound ("monsters/cyberdemon/fire", CHAN_WEAPON);
		}
		CYBR F 12 A_FaceTarget();
		CYBR E 12 BRIGHT
		{
			A_SpawnProjectile("CyberdemonRocket", 48, -16);
			A_PlaySound ("monsters/cyberdemon/fire", CHAN_WEAPON);
		}
		CYBR F 12 A_FaceTarget();
		CYBR E 12 BRIGHT
		{
			A_SpawnProjectile("CyberdemonRocket", 48, -16);
			A_PlaySound ("monsters/cyberdemon/fire", CHAN_WEAPON);
		}
		goto Roam;
	// Moving attack
	MissileWalking:
		CYBR F 0;
		CYBR F 0 A_PlaySound("monsters/cyberdemon/switch");
		CYBR F 12 A_FaceTarget();
		CYBF A 3 { A_Chase(null,null); A_PlaySound("monsters/cyberdemon/hoof"); A_FaceTarget(); }
		CYBF ABBCC 3 { A_Chase(null,null); A_FaceTarget(); }
		CYBF D 3 { A_Chase(null,null); A_PlaySound("spider/walk");  A_FaceTarget(); }
		CYBF D 3 { A_Chase(null,null); A_FaceTarget(); }
		CYBF E 3 BRIGHT
		{
			A_FaceTarget();
			A_Chase(null,null);
			A_SpawnProjectile("CyberdemonRocket", 48, -16);
			A_PlaySound ("monsters/cyberdemon/fire", CHAN_WEAPON);
		}
		CYBF E 3 { A_Chase(null,null); A_PlaySound("monsters/cyberdemon/hoof"); A_FaceTarget(); }
		CYBF BBCC 3 { A_Chase(null,null); A_FaceTarget(); }
		CYBF D 3 { A_Chase(null,null); A_PlaySound("spider/walk"); A_FaceTarget(); }
		CYBF D 3 { A_Chase(null,null); A_FaceTarget(); }
		CYBF E 3 BRIGHT
		{
			A_FaceTarget();
			A_Chase(null,null);
			A_SpawnProjectile("CyberdemonRocket", 48, -16);
			A_PlaySound ("monsters/cyberdemon/fire", CHAN_WEAPON);
		}
		CYBF E 3 { A_Chase(null,null); A_PlaySound("monsters/cyberdemon/hoof"); A_FaceTarget(); }
		CYBF BBCC 3 { A_Chase(null,null); A_FaceTarget(); }
		CYBF D 3 { A_Chase(null,null); A_PlaySound("spider/walk"); A_FaceTarget(); }
		CYBF D 3 { A_Chase(null,null); A_FaceTarget(); }
		CYBR E 12 BRIGHT
		{
			A_FaceTarget();
			A_SpawnProjectile("CyberdemonRocket", 48, -16);
			A_PlaySound ("monsters/cyberdemon/fire", CHAN_WEAPON);
		}
		CYBR F 6 A_FaceTarget();
		goto Roam;
	// Vroom!
	MissileCharge:
		CYBR F 0
		{
			A_FaceTarget();
			
			self.LineTrace(self.angle, 384, 0, TRF_THRUACTORS, offsetz: 8, data:SurfaceData);
			
			if (SurfaceData.HitType == SurfaceData.TRACE_HitWall)
			{
				if (meta_developer)
						Console.printf("Cybie at %i x %i ain't charging into a wall.", self.pos.x, self.pos.y);
					SetStateLabel("MissileWalking");
			}
			
			for (int i = 0; i < 6; i++)
			{
				if (GetZAt ((64*i), 0) != self.floorz)
				{
					if (meta_developer)
						Console.printf("Cybie at %i x %i ain't charging off a ledge.", self.pos.x, self.pos.y);
					SetStateLabel("MissileWalking");
					break;
				}
			}
		}
		// Do the melee attack twice as a visual cue
		CYBR F 0 A_PlaySound("monsters/cyberdemon/switch");
		CYBR P 5 A_FaceTarget();
		CYBR F 10
		{
			A_FaceTarget();
			A_PlaySound("monsters/hellknight/land");
			//A_Explode(128,72,XF_NOTMISSILE|!XF_HURTSOURCE);
			A_SpawnItem("CyberdemonStompDamager",24,0);
			A_SpawnItem("BaronShockwave",24,1);
		}
		CYBF A 4 A_FaceTarget();CYBR P 5 A_FaceTarget();
		CYBR F 10
		{
			A_FaceTarget();
			A_PlaySound("monsters/hellknight/land");
			//A_Explode(128,72,XF_NOTMISSILE|!XF_HURTSOURCE);
			A_SpawnItem("CyberdemonStompDamager",24,0);
			A_SpawnItem("BaronShockwave",24,1);
		}
		CYBF A 8 A_FaceTarget();
		// Chaaarge!
		CYBR F 0 A_PlaySound("monsters/cyberdemon/charge", CHAN_6);
		CYBR B 0 ThrustThing(angle*256/360, 15, 0, 0);
		CYBR BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 
		{
			ThrustThing(angle*256/360, 1, 0, 0);
			A_SpawnItemEx("CyberdemonChargeFire",0,24,0);
			A_SpawnItemEx("CyberdemonChargeFire",0,-24,0);
			A_SpawnItemEx("CyberdemonChargeDamager",48,0,0);
			
			if(vel.z != 0)
			{
				SetStateLabel("MissileChargeAbort");
			}
		}
		CYBR F 0 { self.vel.x = 0; self.vel.y = 0; }
		goto Roam;
	// About to fall off a ledge and break the map! Abort! Abort!
	MissileChargeAbort:
		CYBR F 0 { self.vel.x = 0; self.vel.y = 0; }
		CYBR F 0 A_Stop();
		CYBR F 0 ThrustThing(angle*256/360+128, 8, 0, 0);
		CYBR F 0 ThrustThingZ(0,8,0,0);
		CYBR F 0 A_PlaySound("monsters/cyberdemon/chargebrake", CHAN_6);
		goto Roam;
	Melee:
		CYBR P 0
		{
			// Don't try and stomp on big bosses, it'd just look silly.
			if (target.bBOSS == true)
			{
				SetStateLabel("MissileStanding");
			}
		}
		CYBR P 5 A_FaceTarget();
		CYBR F 10
		{
			A_FaceTarget();
			A_PlaySound("monsters/hellknight/land");
			//A_Explode(128,72,XF_NOTMISSILE|!XF_HURTSOURCE);
			A_SpawnItem("CyberdemonStompDamager",24,0);
			A_SpawnItem("BaronShockwave",24,1);
		}
		CYBF A 4 A_FaceTarget();
		CYBR A 4 A_FaceTarget();
		goto Roam;
	// YOUR PUNY MITTS DO NOT WORK HERE
	Pain.Axe:
	Pain.Fist:
	Pain.BerserkFist:
	Pain:
		CYBR F 0;
		CYBR F 10 A_Pain;
		Goto See+1;
	Death:
		CYBR G 0 A_GiveToTarget("ScoreItem", 10000);
		CYBR G 10 A_Scream;
		CYBR H 10; 
		CYBR G 10 A_PlaySound("monsters/cyberdemon/die2", CHAN_AUTO, 1.0, 0, ATTN_NONE);
		CYBR H 8;
		CYBR I 0
		{
			for (int i = 0; i < 6; i++)
			{
				A_SpawnProjectile ("CyberdemonExplosionParticle", random(8,55), 0, random (0, 360), 2, random (-8, 8));
				A_SpawnProjectile ("CyberdemonExplosionParticle", random(55,112), 0, random (0, 360), 2, random (-8, 8));
			}
			
			A_Quake (4,25,0,750,0);
			
			for (int i = 0; i < 4; i++)
			{
				A_SpawnProjectile ("CyberdemonGibber", random(8,55), 0, random (0, 360), 2, random (32, 64));
				A_SpawnProjectile ("CyberdemonGibber", random(55,112), 0, random (0, 360), 2, random (64, 110));
			}
			
			A_SpawnItem("CyberdemonExplosionScorchMark",0,0);
			A_SpawnItem("CyberdemonExplosionShockwave",0,55);
		}
		CYBR I 5;
		CYBR JKL 5;
		CYBR M 5 A_NoBlocking;
		CYBR N 5;
		CYBR O 30;
		CYBR O -1 A_KillMaster; //A_BossDeath
		Stop;
	}
}

class CyberdemonRocket : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		Damage 20;
		Projectile;
		+RANDOMIZE
		Obituary "$OB_CYBORG";
		Decal "Scorch";
		Damagetype "Explosive";
		scale 0.8;
	}
	
	States
	{
	Spawn:
		CISL A 0 A_PlaySound ("weapons/rocketlauncher/loop", CHAN_BODY, 1.0, 1);
		CISL A 0 A_SpawnItem ("RocketTrailSmoke");
		CISL AAAA 1 Bright;
		Loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("Explosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	}
}

class CyberdemonExplosionParticle : Actor
{
	Default
	{
		Radius 10;
		Height 10;
		Speed 7;
		PROJECTILE;
		+NOCLIP
		+NOGRAVITY
		Decal "Scorch";
	}
	
	States
	{
		Spawn:
		EXPL A 0;
		EXPL A 2 BRIGHT;
		EXPL B 2 BRIGHT ;
		EXPL C 2 BRIGHT ;
		EXPL DEF 2;
		Stop;
	}
}

class CyberdemonGibber : Actor
{
	Default
	{
		radius 20;
		height 56;
		+NOCLIP
	}
	
	states
	{
	Spawn:
        TNT1 A 0;
		TNT1 A 0
		{
			A_SpawnProjectile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnProjectile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnItem("BloodSplat");
		}
		TNT1 A 1 A_SpawnItemEx ("MetaGib1", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 0 A_SpawnItemEx ("MetaGib2", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 0 A_SpawnItemEx ("MetaGib3", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 0 A_SpawnItemEx ("MetaGib4", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 0 A_SpawnItemEx ("MetaGib5", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaGib6", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 0 A_SpawnItemEx ("MetaGib7", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 0 A_SpawnItemEx ("MetaGib8", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 0 A_SpawnItemEx ("MetaGib9", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaGib11", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		TNT1 A 0 A_SpawnItemEx ("MetaGib12", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
		Stop;
    }
}

class CyberdemonChargeFire : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		RenderStyle "Add";
		Alpha 1;
		scale 0.5;
	}
	
	States
	{
	Spawn:
		FIRE A 0;
		FIRE ABCDEH 2 BRIGHT;
		Stop;
	}
}

class CyberdemonExplosionScorchMark : ExplosionScorchMark
{
	Default
	{
		scale 3.0;
	}
}

class CyberdemonExplosionShockwave : Shockwave 
{ 
	Default
	{
		scale 2.0;
	}
	
	states
	{
	DoTheThing:
		SHOK A 1
		{
			A_FadeOut(0.0001);
			A_SetScale(scale.x + 0.1);
		}
		loop;
	}
}

class CyberdemonChargeDamager : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		Obituary "$OB_CYBORG_CHARGE";
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_Explode(128,48,XF_NOTMISSILE|!XF_HURTSOURCE|XF_NOSPLASH);
		Stop;
	}
}

class CyberdemonStompDamager : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		Obituary "$OB_CYBORG_STOMP";
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 
		{
			A_Explode(128,48,XF_NOTMISSILE|!XF_HURTSOURCE|XF_NOSPLASH);
			
			BlockThingsIterator it = BlockThingsIterator.Create(self,96,0);
			while (it.Next())
			{
				Actor mo = it.thing;
				
				if (mo is "PlayerPawn"
				|| mo is "Inventory"
				|| (mo is "MonsterCore" && mo.bBOSS == false))
				{
					if (mo.pos.z == mo.floorz)
						mo.vel.z = 8;
					break;
				}
			}
			
		}
		Stop;
	}
}