/*
----------------------------
MetaDoom
Actor Definitions
Mancubus
----------------------------
*/

// Currently more or less identical to the original Doom monster, behavior-wise.

class MetaMancubus : MonsterCore
{
	Default
	{
		Health 600;
		Radius 48;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 80;
		Monster;
		+FLOORCLIP
		//+BOSSDEATH
		SeeSound "monsters/mancubus/see";
		PainSound "monsters/mancubus/pain";
		DeathSound "monsters/mancubus/die";
		ActiveSound "monsters/mancubus/idle";
		Obituary "$OB_FATSO";
		Species "Fatso";
	}

	States
	{
	Spawn:
		FATT AB 15 A_Look;
		Loop;
	See:
		"####" A 0 A_MetaBleed("FlyingBlood");
		FATT AABBCCDDEEFF 4 A_Chase;
		Loop;
	Missile:
		FATT G 20 { A_FaceTarget(); A_PlaySound("monsters/mancubus/attack"); }
		FATT H 10 BRIGHT A_FatAttack1("MetaFatShot");
		FATT IG 5 A_FaceTarget;
		FATT H 10 BRIGHT A_FatAttack2("MetaFatShot");
		FATT IG 5 A_FaceTarget;
		FATT H 10 BRIGHT A_FatAttack3("MetaFatShot");
		FATT IG 5 A_FaceTarget;
		Goto See;
	Pain:
		FATT J 3;
		FATT J 3 A_Pain;
		Goto See;
    Death:
		FATT K 0 A_SpawnItem("SoulSpawner");
		FATT K 6 A_GiveToTarget("ScoreItem", 500);
		FATT L 6 A_Scream;
		FATT M 6 A_NoBlocking;
		FATT N 6;
		FATT O 6 A_PlaySound("monsters/mancubus/body");
		FATT P 6 ;
		FATT Q 6 A_PlaySound("monsters/mancubus/melt");
		FATT RS 6;
		FATT T -1 A_KillMaster; //A_BossDeath;
	    Stop;
	 Raise:
		FATT R 5; 
		FATT QPONMLK 5;
		Goto See;
	}
}
class MetaFatShot : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 20;
		Damage 8;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 1;
		SeeSound "monsters/mancubus/fire";
		DeathSound "monsters/mancubus/boom";
		decal "Scorch";
	}

	States
	{
	Spawn:
		MANF A 0;
		MANF A 0 A_PlaySound("monsters/imp/fireball/loop", CHAN_BODY, 1.0, 1);
		MANF A 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished");
		MANF A 4 BRIGHT A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		MANF A 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished");
		MANF B 4 BRIGHT A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		Loop;
	Death:
		MISL B 0 A_Quake (3,5,0,750,0);
		MISL B 8 BRIGHT;
		MISL C 6 BRIGHT;
		MISL D 4 BRIGHT;
		Stop;
	Extinguished:
		BAL1 A 0 A_PlaySound("monsters/lostsoul/extinguish");
		TNT1 AAAAA 1 A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		stop;
	}
}

// The Behemoth. Uses the Doom 64 sprites, and a Doom 4-esque flamethrower attack!
class MetaBehemoth : MonsterCore
{
	Default
	{
		Health 600;
		Radius 48;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 80;
		Monster;
		+FLOORCLIP
		//+BOSSDEATH
		SeeSound "monsters/behemoth/see";
		PainSound "monsters/behemoth/pain";
		DeathSound "monsters/behemoth/die";
		ActiveSound "monsters/mancubus/idle";
		Obituary "$OB_FATSO2";
		Species "Fatso";
		scale 0.75;
	}
	
	int user_yeahwelit;

	States
	{
	Spawn:
		2FAT A 0 { user_yeahwelit = 1; }
		2FAT AB 15 A_Look;
		Loop;
	See:
		"####" A 0 A_MetaBleed("FlyingBlood");
		2FAT AABBCCDDEEFF 4 A_Chase;
		Loop;
	Missile:
		2FAT H 20 { A_FaceTarget(); A_PlaySound("monsters/behemoth/attack"); }
		2FAT G 0 BRIGHT A_PlaySound("monsters/behemoth/fire");
		2FAT G 1 BRIGHT { A_FaceTarget(); A_SpawnProjectile("MetaBehemothFireFrontman",48,-32); A_SpawnProjectile("MetaBehemothFireFrontman",48,32); }
		2FAT GGGGGGGGGGGGGGGGGGG 1 BRIGHT
		{
			A_FaceTarget();
			if ( user_yeahwelit = 1)
			{
				A_SpawnProjectile("MetaBehemothFire",48,-32);
				A_SpawnProjectile("MetaBehemothFire",48,32);
				user_yeahwelit = 0;
			} else {
				A_SpawnProjectile("MetaBehemothFireNoLight",48,-32);
				A_SpawnProjectile("MetaBehemothFireNoLight",48,32);
				user_yeahwelit = 1;
			}
		}
		2FAT H 20 A_FaceTarget;
		2FAT H 0 A_Jump(128,"Missile3");
		goto Missile2;
	Missile2:
		2FAT G 0 BRIGHT A_PlaySound("monsters/behemoth/fire");
		2FAT G 1 BRIGHT { A_FaceTarget(); A_SpawnProjectile("MetaBehemothFireFrontman",48,-32,10); A_SpawnProjectile("MetaBehemothFireFrontman",48,32,-10); }
		2FAT GGGGGGGGGGGGGGGGGGG 1 BRIGHT
		{
			A_FaceTarget();
			if ( user_yeahwelit = 1)
			{
				A_SpawnProjectile("MetaBehemothFire",48,-32,10);
				A_SpawnProjectile("MetaBehemothFire",48,32,-10);
				user_yeahwelit = 0;
			} else {
				A_SpawnProjectile("MetaBehemothFireNoLight",48,-32,10);
				A_SpawnProjectile("MetaBehemothFireNoLight",48,32,-10);
				user_yeahwelit = 1;
			}
		}
		2FAT H 20 A_FaceTarget;
		Goto See;
	Missile3:
		2FAT G 0 BRIGHT A_PlaySound("monsters/behemoth/fire");
		2FAT G 1 BRIGHT { A_FaceTarget(); A_SpawnProjectile("MetaBehemothFireFrontman",48,-32,-10); A_SpawnProjectile("MetaBehemothFireFrontman",48,32,10); }
		2FAT GGGGGGGGGGGGGGGGGGG 1 BRIGHT
		{
			A_FaceTarget();
			if ( user_yeahwelit = 1)
			{
				A_SpawnProjectile("MetaBehemothFire",48,-32,-10);
				A_SpawnProjectile("MetaBehemothFire",48,32,10);
				user_yeahwelit = 0;
			} else {
				A_SpawnProjectile("MetaBehemothFireNoLight",48,-32,-10);
				A_SpawnProjectile("MetaBehemothFireNoLight",48,32,10);
				user_yeahwelit = 1;
			}
		}
		2FAT H 20 A_FaceTarget;
		Goto See;
	Pain:
		2FAT I 3;
		2FAT I 3 A_Pain;
		Goto See;
    Death:
		2FAT J 0 A_SpawnItem("SoulSpawner");
		2FAT J 6 A_GiveToTarget("ScoreItem", 600);
		2FAT K 6 A_Scream;
		2FAT L 6 { A_NoBlocking(); A_PlaySound("monsters/mancubus/body"); }
		2FAT M 6
		{
			A_PlaySound("monsters/behemoth/melt");

			for (int i = 0; i < 5; i++)
			{
				A_SpawnProjectile ("FlyingBlood", random(8,32), 0, random (0, 360), 2|CMF_BADPITCH, random (0, 160));
				A_SpawnProjectile ("MetaSploosh", random(8,32), 0, random (0, 360), 2|CMF_BADPITCH, random (0, 160));
			}

			A_SpawnItem("BloodSplat");
		}
		2FAT N 6;
		2FAT O 6;
		2FAT O -1 A_KillMaster; //A_BossDeath;
	    Stop;
	 Raise:
		2FAT ONMLKJ 5;
		Goto See;
	}
}

class MetaBehemothFire : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 20;
		FastSpeed 20;
		DamageFunction (random(1,3));
		Projectile;
		+RANDOMIZE;
		RenderStyle "Add";
		Alpha 1;
		DeathSound "monsters/behemoth/missilehit";
		decal "DoomImpScorch";
		damagetype "Fire";
	}

	States
	{
	Spawn:
		BAL1 A 0;
		BAL1 A 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished");
		BAL1 A 4 BRIGHT A_SpawnItem ("MetaBehemothFireTrail", 0, -8, 0, 0);
		BAL1 B 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished");
		BAL1 B 4 BRIGHT A_SpawnItem ("MetaBehemothFireTrail", 0, -8, 0, 0);
		Loop;
	Death:
		BAL1 CDE 6 BRIGHT;
		Stop;
	Extinguished:
		BAL1 A 0 A_PlaySound("monsters/lostsoul/extinguish");
		TNT1 AAAAA 1 A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		stop;
	}
}

class MetaBehemothFireNoLight : MetaBehemothFire { }

class MetaBehemothFireFrontman : MetaBehemothFire
{
	States
	{
	Spawn:
		BAL1 A 0;
		BAL1 A 0 A_PlaySound("monsters/behemoth/missileloop", CHAN_BODY, 1.0, 1);
		goto Super::Spawn;
	}
}

class MetaBehemothFireTrail : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+ALLOWPARTICLES
		+RANDOMIZE
		RenderStyle "add";
		Alpha 0.5;
		VSpeed 1;
		Mass 5;
		height 1;
		radius 1;
	}

	States
	{
	Spawn:
		LFIR A 0;
		LFIR A 0 A_SetScale(frandom(1.0,1.7),frandom(0.7,1.0));
		LFIR ABCD 2 Bright A_SetScale((scale.x - 0.1),(scale.y + 0.1));
	StartFading:
		LFIR ABCD 2 Bright
		{
			A_SetScale((scale.x - 0.1),(scale.y + 0.1));
			A_FadeOut(0.2);
		}
		loop;
	}
}

// The Cyber-Manc from Doom 4. Launches mortar shots that explode shortly after
// hitting a surface if they miss, and explodes (harmlessly to the player) shortly
// after dying.

class MetaCyberManc : MonsterCore
{
	Default
	{
		Health 700;
		Radius 48;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 80;
		Monster;
		+FLOORCLIP
		//+BOSSDEATH
		SeeSound "monsters/cybermanc/see";
		PainSound "monsters/cybermanc/pain";
		DeathSound "monsters/cybermanc/die1";
		ActiveSound "monsters/cybermanc/idle";
		Obituary "$OB_CFATSO";
		Species "Fatso";
		scale 0.85;
		yscale 0.75;
	}

	States
	{
	Spawn:
		CFAT G 15 A_Look;
		Loop;
	See:
		"####" A 0 A_MetaBleed("FlyingBlood");
		CFAT AABBCCDDEEFF 4 A_Chase;
		Loop;
	Missile:
		CFAT G 20 { A_FaceTarget(); A_PlaySound("monsters/cybermanc/attack"); }
		CFAT H 10 BRIGHT { A_FaceTarget(); A_SpawnProjectile("CyberMancShot",48,-32); A_SpawnProjectile("CyberMancShot",48,32); }
		CFAT IG 5 A_FaceTarget;
		CFAT H 10 BRIGHT { A_FaceTarget(); A_SpawnProjectile("CyberMancShot",48,-32,10); A_SpawnProjectile("CyberMancShot",48,32,-10); }
		CFAT IG 5 A_FaceTarget;
		CFAT G 0 A_MonsterRefire(60,"See");
		Goto Missile+1;
	Pain:
		CFAT P 3;
		CFAT P 3 A_Pain;
		Goto See;
    Death:
		CFAT Q 0 A_SpawnItem("SoulSpawner");
		CFAT Q 6 A_GiveToTarget("ScoreItem", 700);
		CFAT R 6 A_Scream;
		CFAT S 6 A_NoBlocking;
		CFAT T 6 A_PlaySound("monsters/cybermanc/die2");
		CFAT U 32 A_KillMaster; //A_BossDeath;
		CFAT Z 4 BRIGHT A_PlaySound("monsters/cybermanc/boop");
		CFAT U 24;
		CFAT Z 4 BRIGHT A_PlaySound("monsters/cybermanc/boop");
		CFAT U 16;
		CFAT Z 4 BRIGHT A_PlaySound("monsters/cybermanc/boop");
		CFAT U 8;
		CFAT Z 4 BRIGHT A_PlaySound("monsters/cybermanc/boop");
		CFAT U 2;
		CFAT Z 4 BRIGHT A_PlaySound("monsters/cybermanc/boop");
		CFAT U 2;
		CFAT Z 4 BRIGHT A_PlaySound("monsters/cybermanc/boop");
		CFAT U 2;
		CFAT Z 4 BRIGHT A_PlaySound("monsters/cybermanc/boop");
		CFAT U 2;
		CFAT Z 4 BRIGHT A_PlaySound("monsters/cybermanc/boop");
		CFAT V 6 
		{
			A_PlaySound("monsters/cybermanc/die3");
			
			for (int i = 0; i < 5; i++)
			{
				A_SpawnProjectile ("FlyingBlood", random(8,64), 0, random (0, 360), 2|CMF_BADPITCH, random (0, 160));
				A_SpawnProjectile ("MetaSploosh", random(8,64), 0, random (0, 360), 2|CMF_BADPITCH, random (0, 160));
			}
			
			A_SpawnItemEx ("MetaGib1", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib2", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib3", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib4", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib5", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib6", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib7", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib8", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib9", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib10", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib11", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib12", 1, 1, random(32,64), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("CyberMancDeathExplosion", 0, 0, 32);
			
			A_SpawnItem("BloodSplat");
		}
		CFAT W 6;
		CFAT X -1;
	    Stop;
	 Raise:
		FATT R 5;
		FATT QPONMLK 5;
		Goto See;
	}
}

class CyberMancDeathExplosion : Actor
{
	Default
	{
		Radius 10;
		Height 10;
		Speed 5;
		PROJECTILE;
		+NOCLIP
		Decal "Scorch";
		damagetype "NoHarmPlayer";
	}

	States
	{
		Spawn:
		EXPL A 0;
		EXPL A 2 BRIGHT
		{
			//A_PlaySound("weapons/explosion", CHAN_WEAPON);
			A_Explode();
			A_Quake (3,10,0,750,0);
			A_SpawnItem("Shockwave",0,0);
			
			if (pos.z <= (floorz + 64))
			{
				A_SpawnItem("ExplosionScorchMark",0,0);
			}
		}
		EXPL B 2 BRIGHT;
		EXPL C 2 BRIGHT;
		EXPL DEF 2;
		Stop;
	}
}

class CyberMancShot : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 30;
		Damage 8;
		Projectile;
		+RANDOMIZE
		SeeSound "monsters/cybermanc/fire";
		DeathSound "monsters/cybermanc/hit";
		decal "greenbloodsmear";
		scale 1.25;
	}

	States
	{
	Spawn:
		BAL7 A 0;
		BAL7 A 0 ThrustThingZ(0,8,0,1);
		BAL7 A 0 A_PlaySound("monsters/imp/fireball/loop", CHAN_BODY, 1.0, 1);
		BAL7 ABAB 4 BRIGHT A_SpawnItem ("MetaBaronBallEffect", 0, 0, 0, 0);
		BAL7 A 0 { bNOGRAVITY = 0; }
		goto SpawnFall;
	SpawnFall:
		BAL7 AB 4 BRIGHT A_SpawnItem ("MetaBaronBallEffect", 0, 0, 0, 0);
		loop;
	XDeath:
	Crash:
		BAL7 B 0
		{
			A_Quake (3,5,0,750,0);
		
			for (int i = 0; i < 4; i++)
			{
				A_SpawnProjectile ("MetaSplooshGreen", random(0,8), 0, random (0, 360), 2, random (0, 160));
			}		
		}
		BAL7 CDE 6 BRIGHT;
		stop;
	Death:
		BAL7 B 0 
		{
			A_Quake (3,5,0,750,0);
		
			for (int i = 0; i < 3; i++)
			{
				A_SpawnProjectile ("MetaSplooshGreen", random(0,8), 0, random (0, 360), 2, random (0, 160));
			}
		}
		BAL7 B 0 A_SpawnItem("CyberMancBomb");
		BAL7 CDE 6 BRIGHT;
		stop;
	}
}

class CyberMancBomb : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		-SOLID
		+NOGRAVITY
	}

	States
	{
	Spawn:
		CBOM ABCDABCDAB 4 BRIGHT;
		goto Death;
	Death:
		CBOM B 0 
		{
			A_Quake (3,5,0,750,0);
		
			for (int i = 0; i < 4; i++)
			{
				A_SpawnProjectile ("MetaSplooshGreen", random(0,8), 0, random (0, 360), 2, random (0, 160));
			}		
		}
		CBOM B 0 A_Explode (8,64);
		CBOM B 0 A_PlaySound("monsters/cybermanc/bomb",7);
		CBOM EFG 6 BRIGHT;
		TNT1 A 35; // gross sound cutoff fix hack
		stop;
	}
}