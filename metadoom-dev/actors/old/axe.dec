ACTOR MetaAxePickup : WeaponGiver
{
	DropItem "MetaAxe"
	Inventory.PickupMessage "$GOTAXE"
	Inventory.PickupSound "weapons/axe/get"
	States
	{
	Spawn:
		AXEG Z 0
		AXEG Z 0 A_ChangeFlag ("DROPPED", 0)
		AXEG Z -1
		Stop
	}
}

ACTOR MetaAxe : MetaDoomWeapon
{
	Weapon.SelectionOrder 2400
	Weapon.Kickback 100
	Obituary "$OB_AXE"
	Tag "$TAGAXE"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	Inventory.PickupMessage "$GOTAXE"
	Inventory.PickupSound "weapons/axe/get"
	States
	{
	Ready:
		AXEG A 1 A_WeaponReady
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		Loop
	Deselect:
		AXEG A 1 
		AXEG A 0 A_JumpIf(health <= 0, "DeadDude")
		AXEG A 1 Offset(0,33) A_PlaySound("weapons/swap")
		AXEG A 1 Offset(0,35)
		AXEG A 1 Offset(0,39)
		AXEG A 1 Offset(0,44)
		AXEG A 1 Offset(0,49)
		AXEG A 1 Offset(0,55)
		AXEG A 1 Offset(0,60)
		AXEG A 1 Offset(0,70)
		AXEG A 1 Offset(0,80)
		AXEG A 1 Offset(0,90)
	FinishDeselect:
		AXEG A 0 A_Lower
		Loop
	Select:
		AXEG A 1 Offset(0,90) A_PlaySound("weapons/axe/use")
		AXEG A 1 Offset(0,80)
		AXEG A 1 Offset(0,70)
		AXEG A 1 Offset(0,60)
		AXEG A 1 Offset(0,55)
		AXEG A 1 Offset(0,49)
		AXEG A 1 Offset(0,44)
		AXEG A 1 Offset(0,39)
		AXEG A 1 Offset(0,35)
		AXEG A 1 Offset(0,33)
		AXEG A 1 Offset(0,32)
		goto Ready
	AltFire:
	Fire:
		TNT1 A 0 A_PlaySound("weapons/swap", CHAN_AUTO, 0.5)
		AXEG BCDEFG 1 
		TNT1 A 2
		AXEG I 1 
		{
			A_PlaySound("weapons/axe/fire");
			
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		}
		AXEG JK 1
		AXEG L 1 A_CustomPunch(7 * random(1, 5), TRUE, CPF_NOTURN,"MetaAxePuff",80,0,0,"BasicArmorBonus","weapons/axe/hit")
		AXEG MNO 1
		TNT1 A 1
		TNT1 A 1 Offset(0,32)
		TNT1 A 8 Offset(0,90)
		AXEG A 1 Offset(0,90)
		AXEG A 1 Offset(0,70)
		AXEG A 1 Offset(0,55)
		AXEG A 1 Offset(0,44)
		AXEG A 1 Offset(0,35)
		AXEG A 1 Offset(0,33)
		AXEG A 1 Offset(0,32)
		Goto Ready
	Spawn:
		AXEG Z -1
		Stop
	}
}

ACTOR MetaAxePuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	damagetype Axe
	RenderStyle Translucent
	Alpha 0.5
	VSpeed 1
	Mass 5
	AttackSound "weapons/axe/hitwall"
	SeeSound "weapons/axe/hit"
	Obituary "$OB_AXE"
	States
	{
	Spawn:
		PUFF A 4 Bright
		PUFF B 4
	Melee:
		PUFF CD 4
		Stop
	}
}