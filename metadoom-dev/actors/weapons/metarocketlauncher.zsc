/*
----------------------------
MetaDoom
Actor Definitions
Rocket Launcher
----------------------------
*/

class RLDetonator : _Boolean {}

class MetaRocketLauncherPickup : WeaponGiver
{
	Default
	{
		DropItem "MetaRocketLauncher";
		Inventory.PickupMessage "$GOTLAUNCHER";
		Inventory.PickupSound "weapons/rocketlauncher/get";
		scale 0.75;
	}
	
	States
	{
	Spawn:
		RLAU Z 0;
		RLAU Z 0 { bDROPPED = 0; }
		RLAU Z -1;
		Stop;
	}
}

class MetaRocketLauncher : MetaDoomWeapon
{
	Default
	{
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "RocketAmmo";
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$GOTLAUNCHER";
		Tag "$TAG_ROCKETLAUNCHER";
		Weapon.YAdjust 10;
		Inventory.PickupSound "weapons/rocketlauncher/get";
		scale 0.75;
		+WEAPON.NOALERT
		Weapon.BobRangeX 0.5;
		Weapon.BobRangey 0.75;
		//$Category "Weapons/MetaDoom"
		//$Title "Rocket Launcher"
		//$NotAngled
		//$Color 14
	}
	
	States
	{
	Ready:
		RLAU A 1 A_WeaponReady;
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		Loop;
	Deselect:
		RLAU A 1;
		RLAU A 0 A_JumpIf(health <= 0, "DeadDude");
		RLAU A 1 Offset(0,33) A_PlaySound("weapons/swap");
		RLAU A 1 Offset(0,35);
		RLAU A 1 Offset(0,39);
		RLAU A 1 Offset(0,44);
		RLAU A 1 Offset(0,49);
		RLAU A 1 Offset(0,55);
		RLAU A 1 Offset(0,60);
		RLAU A 1 Offset(0,70);
		RLAU A 1 Offset(0,80);
		RLAU A 1 Offset(0,90);
	FinishDeselect:
		RLAU A 0 A_Lower;
		Loop;
	Select:
		RLAU A 1 Offset(0,90) A_PlaySound("weapons/rocketlauncher/use");
		RLAU A 1 Offset(0,80);
		RLAU A 1 Offset(0,70);
		RLAU A 1 Offset(0,60);
		RLAU A 1 Offset(0,55);
		RLAU A 1 Offset(0,49);
		RLAU A 1 Offset(0,44);
		RLAU A 1 Offset(0,39);
		RLAU A 1 Offset(0,35);
		RLAU A 1 Offset(0,33);
		RLAU A 1 Offset(0,32);
		goto Ready;
	Fire:
		RLAU B 0 A_GunFlash;
		RLAU A 0 
		{
			A_FireProjectile ("MetaLauncherRocket");
			A_PlaySound ("weapons/rocketlauncher/fire", CHAN_WEAPON);
		
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		
			A_GunFlash();
			A_AlertMonsters();
		}
		RLAU B 1 bright Offset(0,45) A_SetPitch(pitch-2);
		RLAU B 1 bright Offset(0,48) A_SetPitch(pitch-2);
		RLAU B 1 bright Offset(0,47);
		RLAU C 1 bright Offset(0,46) A_SetPitch(pitch+1);
		RLAU C 1 bright Offset(0,44) A_SetPitch(pitch+1);
		RLAU C 1 bright Offset(0,42) A_SetPitch(pitch+1);
		RLAU D 1 bright Offset(0,38) A_SetPitch(pitch+1) ;
		RLAU D 1 bright Offset(0,36) A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB);
		RLAU D 1 Offset(0,34) A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB);
		RLAU A 1 Offset(0,33) A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB);
		RLAU A 1 Offset(0,32) A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB);
		RLAU A 8 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB);
		Goto Ready;
	AltFire:
		RLAU A 0;
		RLAU A 1 Offset(0,34)
		{
				A_GiveInventory("RLDetonator", 1);
				A_PlaySound("weapons/rocketlauncher/deto", CHAN_BODY);
		}
		RLAU A 1 Offset(0,33);
		RLAU A 1 Offset(0,32) A_TakeInventory("RLDetonator", 1);
		RLAU A 10;
		goto Ready;
	Flash:
		TNT1 A 3 Bright A_Light1;
		TNT1 A 4 Bright;
		TNT1 AA 4 Bright A_Light2;
		Goto LightDone;
	Spawn:
		RLAU Z -1;
		Stop;
	}
}

class MetaLauncherRocket : Weapon
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 30;
		Damage 20;
		Projectile;
		+RANDOMIZE
		Obituary "$OB_MPROCKET";
		Decal "Scorch";
		Damagetype "Explosive";
	}
	
	States
	{
	Spawn:
		MISL A 0 A_PlaySound ("weapons/rocketlauncher/loop", CHAN_BODY, 1.0, 1);
		MISL A 0 A_SpawnItem ("RocketTrailSmoke", 0, 6, 0, 0);
		MISL AAAA 1 Bright A_JumpIfInTargetInventory("RLDetonator",1,"AltDeath");
		Loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("Explosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	AltDeath:
		TNT1 A 0 A_SpawnItemEx("Explosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS);
		TNT1 A 0 A_PlaySound("weapons/rocketlauncher/split", CHAN_6);
		TNT1 A 0
		{
			for (int i = 0; i < 12; i ++)
			{
				A_SpawnProjectile("MetaRocketFragment",6,0,(30 * i),CMF_AIMDIRECTION,random(0,16));
			}
		}
		Stop;
	}
}

class MetaRocketFragment : Actor
{
	Default
	{
		+DOOMBOUNCE
		PROJECTILE;
		-NOGRAVITY
		-ACTIVATEIMPACT
		+NOTELEPORT
		-TELESTOMP
		-SOLID
		+ROLLSPRITE
		+ROLLCENTER
		+RIPPER
		Radius 2;
		Damage 3;
		bouncefactor 0.5;
		SeeSound "weapons/rocketlauncher/fragment";
		gravity 0.25;
		Speed 2;
		scale 0.5;
	}
	
	States
	{
	Spawn:
		RSHD A 0;
		RSHD ABCD 0 A_Jump(128, "Setup");
		goto Setup;
	Setup:
		"####" # 0 ThrustThingZ (0, random(32,64), 0, 1);
		"####" # 0 ThrustThing(random(0,360), 8, 1, 0);
	ChangeDir:
		"####" # 0 A_Jump(128,"Alt") ;
		goto See;
	See:
		"####" # 0 ThrustThingZ(0,6,1,1);
		"####" # 0 A_SpawnItem ("RocketFragmentTrailSmoke", 0, 1, 0, 0);
		"####" # 1 A_SetRoll((roll + 5));
		loop;
	Alt:
		"####" # 0 ThrustThingZ(0,6,1,1);
		"####" # 0 A_SpawnItem ("RocketFragmentTrailSmoke", 0, 1, 0, 0);
		"####" # 1 A_SetRoll((roll - 5));
		loop;
	Bounce.Floor:
		//"####" # 0 A_SpawnItem("BloodSplat")
		goto ChangeDir;
	Bounce.Wall:
		goto ChangeDir;
	Death:
		"####" # 1 A_FadeOut(0.02);
		Wait;
	XDeath:
		TNT1 A 0;
		Stop;
    }
}