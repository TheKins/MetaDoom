/*
----------------------------
MetaDoom
Actor Definitions
Shotgun
----------------------------
*/

class MetaShotgunPickup : WeaponGiver
{
	Default
	{
		DropItem "MetaShotgun";
		Inventory.PickupMessage "$GOTSHOTGUN";
		Inventory.PickupSound "weapons/shotgun/get";
	}
	
	States
	{
	Spawn:
		STG1 Z 0;
		STG1 Z 0 { bDROPPED = 0; }
		STG1 Z -1;
		Stop;
	}
}

class MetaShotgun : MetaDoomWeapon
{
	Default
	{
		Weapon.SelectionOrder 1300;
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 4;
		Weapon.AmmoGive 8;
		Weapon.AmmoType1 "Shell";
		Weapon.AmmoType2 "Shell";
		Inventory.PickupMessage "$GOTSHOTGUN";
		Obituary "$OB_METASHOTGUN_SHELL";
		Tag "$TAG_SHOTGUN";
		Inventory.PickupSound "weapons/shotgun/get";
		+WEAPON.ALT_AMMO_OPTIONAL
		Weapon.UpSound "weapons/shotgun/cock1";
		Weapon.SlotNumber 2;
		//$Category "Weapons/MetaDoom"
		//$Title "Combat Shotgun"
		//$NotAngled
		//$Color 14
	}
	
	States
	{
	Ready:
		STG1 A 1 A_WeaponReady;
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		Loop;
	Deselect:
		STG1 A 0 A_JumpIf(health <= 0, "DeadDude");
		STG1 A 0 A_PlaySound("weapons/swap");
	FinishDeselect:
		STG1 A 1 A_SmoothLower();
		Loop;
	Select:
		STG1 A 0 A_OverlayScale(PSP_WEAPON, 1, 1);
		STG1 A 1 A_SmoothRaise();
		loop;
	Fire:
		SSH1 A 3;
		SSH1 B 3 bright 
		{
			A_FireBullets (5.6, 0, 7, 5, "MetaBulletPuff");
			A_PlaySound ("weapons/shotgun/fire", CHAN_WEAPON);
			
			if (CountInv("PowerMetaDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
			
			A_GunFlash();
			A_Overlay(14, "Overlay.Fire");
		}
		SSH1 C 2 bright;
		SSH1 D 1;
		SSH1 EFG 2;
		SSH1 HIJ 2 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		SSH1 K 4
		{
			A_PlaySound ("weapons/shotgun/cock1", CHAN_BODY);
			if (meta_perf_spentbrass == 1)
			{
				A_FireProjectile("ShotCaseSpawn",5,0,6,4);
			}
			A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		}
		SSH1 LMNOP 2 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		SSH1 Q 2 A_PlaySound ("weapons/shotgun/cock2", CHAN_BODY);
		//STG1 A 8 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		Goto Ready;
	AltFire:
		STG1 A 0 A_JumpIfInventory("Shell",4,"GrenadeShot");
		STG1 A 10 { A_Overlay(14, "Overlay.GenericButtonPress"); A_PlaySound("weapons/shotgun/dry", CHAN_BODY); }
		goto Ready;
	GrenadeShot:
		// Adding interpolation to this one makes it look all shaky, so let's
		// leave it as the old code for now.
		STG1 A 3;
		STG1 A 0 
		{
			A_FireProjectile("MetaShotgunGrenade",0,0,0,0,FPF_NOAUTOAIM);
			A_PlaySound ("weapons/shotgun/gfire", CHAN_WEAPON);
			
			if (CountInv("PowerMetaDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
			
			A_GunFlash();
			A_TakeInventory("Shell", 4);
		}
		STG1 J 1 bright Offset(0,45) A_SetPitch(pitch-4);
		STG1 J 1 bright Offset(0,48) A_SetPitch(pitch-4);
		STG1 K 2 bright Offset(0,48);
		STG1 K 1 bright Offset(0,46) A_SetPitch(pitch+1);
		STG1 L 1 bright Offset(0,44) A_SetPitch(pitch+1);
		STG1 L 1 Offset(0,42) A_SetPitch(pitch+1);
		STG1 D 1 Offset(0,38) A_SetPitch(pitch+1);
		STG1 D 1 Offset(0,36) A_SetPitch(pitch+2);
		STG1 A 1 Offset(0,34) A_SetPitch(pitch+2);
		STG1 A 1 Offset(0,33);
		STG1 A 3 Offset(0,32);
		STG1 M 1 Offset(-4,32);
		STG1 M 1 Offset(0,32);
		STG1 N 1 Offset(-4,32);
		STG1 N 1 Offset(0,32);
		STG1 O 1 Offset(-4,32);
		STG1 O 1 Offset(0,32);
		STG1 P 1 Offset(-4,34) 
		{
			A_PlaySound ("weapons/shotgun/cock1", CHAN_BODY);
		}
		STG1 P 10 Offset(0,32);
		STG1 P 1 Offset(4,34);
		STG1 O 1 Offset(0,32);
		STG1 O 1 Offset(4,32);
		STG1 N 1 Offset(0,32);
		STG1 N 1 Offset(-4,32);
		STG1 M 1 Offset(-4,32);
		STG1 M 1 Offset(0,32);
		STG1 A 1 Offset(0,35) A_PlaySound ("weapons/shotgun/cock2", CHAN_BODY);
		STG1 A 1 Offset(0,34);
		STG1 A 1 Offset(0,33);
		STG1 A 1 Offset(0,32);
		STG1 A 8 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		Goto Ready;
	Flash:
		TNT1 A 4 Bright A_Light1;
		TNT1 A 3 Bright A_Light2;
		Goto LightDone;
	Spawn:
		STG1 Z -1;
		Stop;
		
	Overlay.Fire:
		TNT1 B 1 { A_WeaponOffset(0,45, WOF_INTERPOLATE); A_SetPitch(pitch-2, SPF_INTERPOLATE); }
		TNT1 B 1 { A_WeaponOffset(0,42,WOF_INTERPOLATE); A_SetPitch(pitch-2, SPF_INTERPOLATE); }
		TNT1 C 1 A_WeaponOffset(0,40,WOF_INTERPOLATE);
		TNT1 C 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		TNT1 D 1 { A_WeaponOffset(0,36,WOF_INTERPOLATE); A_SetPitch(pitch+1, SPF_INTERPOLATE); }
		TNT1 E 2 { A_WeaponOffset(0,34,WOF_INTERPOLATE); A_SetPitch(pitch+1, SPF_INTERPOLATE); }
		TNT1 F 2 { A_WeaponOffset(0,32,WOF_INTERPOLATE); A_SetPitch(pitch+1, SPF_INTERPOLATE); }
		TNT1 G 2 A_SetPitch(pitch+1, SPF_INTERPOLATE);
		TNT1 A -1;
		stop;
	}
}

class MetaShotgunGrenade : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 40;
		Damage 10;
		Projectile;
		+RANDOMIZE
		-NOGRAVITY
		Decal "Scorch";
		Obituary "$OB_METASHOTGUN_GREN";
	}
	
	States
	{
	Spawn:
		SGRN A 1 Bright A_SpawnItem ("GrenadeTrailSmoke", 0, 0, 0, 0);
		Loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("ShotgunGrenadeExplosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	}
}

class ShotgunGrenadeExplosion : MetaExplosion
{
	Default
	{
		scale 0.5;
		Obituary "$OB_METASHOTGUN_GREN";
	}
	
	States
	{
		Spawn:
		EXPL A 0;
		EXPL A 2 BRIGHT
		{
			A_PlaySound("weapons/shotgun/grenade", CHAN_WEAPON);
			A_Explode(); //(72,128);
			A_Quake (3,5,0,750,0);
			A_SpawnItem("ShockwaveSmall");
		}
		EXPL B 2 BRIGHT;
		EXPL C 2 BRIGHT;
		EXPL DEF 2;
		Stop;
	}
}

class ShockwaveSmall : Shockwave
{
	Default
	{
		radius 1;
		height 1;
		scale 0.5;
	}
}