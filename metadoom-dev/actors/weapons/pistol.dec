/*
----------------------------
MetaDoom
Actor Definitions
Pistol
----------------------------
*/

actor PistolFlashlight: _Boolean {}
actor PistolAmmoCoupon: _Boolean {}

ACTOR MetaPistolPickup : WeaponGiver
{
	DropItem "MetaPistol"
	Inventory.Pickupmessage "$GOTPISTOL"
	Inventory.PickupSound "weapons/pistol/get"
	States
	{
	Spawn:
		PIST Z 0
		PIST Z 0 A_ChangeFlag ("DROPPED", 0)
		PIST Z -1
		Stop
	}
}

ACTOR MetaPistol : MetaDoomWeapon
{
	Weapon.SelectionOrder 1900
	Obituary "$OB_METAPISTOL"
	+WEAPON.WIMPY_WEAPON
	Inventory.Pickupmessage "$GOTPISTOL"
	Inventory.PickupSound "weapons/pistol/get"
	Tag "$TAGPISTOL"
	+INVENTORY.ALWAYSPICKUP
	+WEAPON.NOALERT
	+INVENTORY.UNTOSSABLE
	States
	{
	Ready:
		PIST A 1 A_WeaponReady
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		Loop
	Deselect:
		PIST A 1
		PIST A 0 A_JumpIf(health <= 0, "DeadDude")
		PIST A 1 Offset(0,33)
		{			
			A_PlaySound("weapons/swap");
			
			if (CountInv("PistolFlashlight") == 1)
			{
				A_TakeInventory("PistolFlashlight", 1);
			}			
		}
		PIST A 1 Offset(0,35)
		PIST A 1 Offset(0,39)
		PIST A 1 Offset(0,44)
		PIST A 1 Offset(0,49)
		PIST A 1 Offset(0,55)
		PIST A 1 Offset(0,60)
		PIST A 1 Offset(0,70)
		PIST A 1 Offset(0,80)
		PIST A 1 Offset(0,90)
	FinishDeselect:
		PIST A 0 A_Lower
		Loop
	Select:
		PIST A 1 Offset(0,90) A_PlaySound("weapons/pistol/use")
		PIST A 1 Offset(0,80)
		PIST A 1 Offset(0,70)
		PIST A 1 Offset(0,60)
		PIST A 1 Offset(0,55)
		PIST A 1 Offset(0,49)
		PIST A 1 Offset(0,44)
		PIST A 1 Offset(0,39)
		PIST A 1 Offset(0,35)
		PIST A 1 Offset(0,33)
		PIST A 1 Offset(0,32)
		PIST A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto Ready
	Fire:
		PIST A 0		
		{
			if (GetCVar ("meta_pistolammo") == 1 && CountInv("Clip") == 0)
			{
				return state("NoAmmo");
			}
			
			return state(""); // this is dumb. why do i need this?
		}
		PIST A 4
		PIST B 1 bright
		{
			if (GetCVar ("meta_pistolammo") == 0 || GetCVar ("meta_pistolammo") == 1 && CountInv("Clip") > 0)
			{
				A_FireCustomMissile("PistolTracer");
				if (GetCVar ("meta_pistolammo") == 1)
				{
					A_TakeInventory("Clip", 1);
				}
				A_PlaySound("weapons/pistol/fire", CHAN_WEAPON);
				
				if (CountInv("PowerDamage") > 0)
				{
					A_PlaySound("powerups/quad/use", CHAN_5);
				}
				
				A_GunFlash;
				A_AlertMonsters;
			}
		}
		PIST B 1 bright Offset(0,38)
		PIST C 1 bright Offset(0,42)
		PIST C 1 bright Offset(0,43) A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		PIST D 1 bright Offset(0,38) A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		PIST D 1 Offset(0,35) A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		PIST E 1 Offset(0,33) A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		PIST E 1 Offset(0,32) A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		PIST FF 1  A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		Goto Ready
	NoAmmo:
		PIST A 0
		PIST A 1 Offset(0,34) A_PlaySound("weapons/pistol/dry", CHAN_BODY)
		PIST A 1 Offset(0,33)
		PIST A 1 Offset(0,32)
		PIST A 10
		goto Ready
	AltFire:
		PIST A 0
		PIST A 1 Offset(0,34)
		{
			if (CountInv("PistolFlashlight") == 0)
			{
				A_GiveInventory("PistolFlashlight", 1);
				A_PlaySound("weapons/flashlight/on", CHAN_BODY);
			} else {
				A_TakeInventory("PistolFlashlight", 1);
				A_PlaySound("weapons/flashlight/off", CHAN_BODY);
			}
		}
		PIST A 1 Offset(0,33)
		PIST A 1 Offset(0,32)
		PIST A 10
		goto Ready
	Flash:
		TNT1 A 4 Bright A_Light1
		Goto LightDone
		TNT1 A 4 Bright A_Light1
		Goto LightDone
 	Spawn:
		PIST Z -1
		Stop
	}
}

ACTOR PistolTracer : FastProjectile
{
	Alpha 0.90
	RenderStyle Add
	Radius 2
	Height 2
	damage 5
	Speed 75
	PROJECTILE
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	Scale 0.4
	MissileType PistolTracerTrail
	MissileHeight 8
	Decal BulletChip
	Obituary "$OB_METAPISTOL"
	States
	{
	Spawn:
		PLSE E 1 BRIGHT
		Loop
	Death:
	Crash:
		PUFF A 0 A_PlaySound("weapons/pistol/hitwall", CHAN_BODY, frandom(0.5,1.0))
		PUFF A 2 BRIGHT A_SpawnDebris("PistolTracerSpark")
		PUFF B 2 BRIGHT A_SetTranslucent(0.9,0)
		PUFF CD 2 BRIGHT
		Stop
	XDeath:
		TNT1 A 3 A_PlaySound("weapons/pistol/hit")
		Stop
	}
}

ACTOR PistolTracerTrail
{ 
	Alpha 0.75
	RenderStyle Add
	Speed 0
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	Scale 0.4
	States
	{
	Spawn:
		PLSE E 1 BRIGHT A_FadeOut(0.3)
		Loop
	}
}

ACTOR PistolTracerSpark
{
	Health 4
	radius 3
	height 6
	speed .1
	RenderStyle ADD
	Alpha		1
	Scale 0.5
	Mass		0
	+missile
	+doombounce
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	States
	{
		PLSE E 1
		PLSE EEEEE 1  Bright A_SetTranslucent(.8,1)
		PLSE EEEEEE 1  Bright A_SetTranslucent(.6,1)
		PLSE EEEEEEEE 1  Bright A_SetTranslucent(.4,1)
		PLSE EEEEEEEEEE 1 Bright A_SetTranslucent(.2,1)
		Stop
	}
}